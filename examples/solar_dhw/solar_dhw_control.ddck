***************************************************
**Solar controller
**************************************************

*****************************************
** Contact person : Jeremias Schmidli   
** Creation date  : 28.04.2020 
** Last changes   : 22.06.2020, JS, change tSetAux to 50 and TdhwSet to 45 
*****************************************

***************************************************************************
** Description: 
** custom controlls for the solar dhw example system 
** TODO:
***************************************************************************


***********************************
** Control Temperatures
***********************************

EQUATIONS 6
myTCollOut = TCollOut          !@dependencyDdck
mySenTesSolarTop = Tsen9_Tes1 !@dependencyDdck
mySenTesSolarBot = Tsen1_Tes1 !@dependencyDdck
mySenTesAux = Tsen8_Tes1 !@dependencyDdck
tSetAux = 50
TdhwSet = 45

***********************************
** Begin TYPE AUXILIAR CONTROLLER
***********************************

CONSTANTS 2
dtAuxOn = -3     ! Upper dead band coll. pump ON
dtAuxOff = -7     ! Lower dead band coll. pump OFF

UNIT 27 TYPE 2  ! Tset - UDB < Tmonitored < Tset - LDB
PARAMETERS 2     
5     ! 1 No. of oscillations
98   ! 2 High limit cut-out
INPUTS 6     
tSetAux        ! Setpoint temperature, Tset
mySenTesAux ! Temperature to watch, Tmonitored
mySenTesAux ! High limit monitoring temperature
auxOn          ! 70,1 Input control function
dtAuxOn        ! Turn on temperature difference, Upper dead band, UDB dT
dtAuxOff       ! Turn off temperature difference, Lower dead band dT, LDB
0.0 20.0 0.0 0 dtAuxOn dtAuxOff      
EQUATIONS 1  
auxOn = [27,1]


***********************************
** Begin TYPE COLLECTOR CONTROLLER
***********************************

CONSTANTS 2
dtColOn = 7    ! Upper dead band coll. pump ON, JS: was 10 before, yield is higher with 7, to be improved
dtColOff = 3    ! Lower dead band coll. pump OFF


UNIT 28 TYPE 2  
PARAMETERS 2     
5     ! 1 No. of oscillations
98   ! 2 High limit cut-out
INPUTS 6     
myTCollOut     ! Upper input temperature Th
mySenTesSolarBot ! Lower input temperature Tl
mySenTesSolarTop ! Monitoring temperature Tin
puColOn        ! 70,1 Input control function
dtColOn        ! Upper dead band dT
dtColOff       ! Lower dead band dT
0.0 20.0 0.0 0 dtColOn dtColOff      
EQUATIONS 1  
puColOn = [28,1]*LT(myTcollOut,120)


EQUATIONS 3
*** Massflowrates
MfrPuCol = puColOnRc*MfrCPriNom
MfrWTapBottom = MfrDHWset
MfrDhwDemand = ABS(MfrPiDhwDemand)

CONSTANTS 1
T_set_MixDhw=TdhwSetProfile

UNIT 441 TYPE 811 ! Passive Divider for heating 
PARAMETERS 1
5 !Nb.of iterations before fixing the value 
INPUTS 4 
TPiTesDhwOut
TPiDhwMix
MfrDhwDemand
T_set_MixDhw
*** INITIAL INPUT VALUES
35.0 21.0 800.0 T_set_MixDhw
EQUATIONS 1
xFracMixDhw =  1.-[441,5] 


***********************************
** Begin TYPE Recall
***********************************

UNIT 18 TYPE 993 
PARAMETERS 1     
2   ! 1: number of variables to be remembered    
INPUTS 2     
auxOn puColOn     
0.0  0.0  
EQUATIONS 2    ! outputs of Input Value Recall
auxOnRc = [18,1]      
puColOnRc = [18,2]  


*************
**END Solar controller
************




