*******************************
**BEGIN Type1.ddck 
*******************************

*****************************************
** Contact person : Dani Carbonell    
** Creation date  : 10.01.2010
** Last changes   : 04.03.2022
*****************************************

***************************************************************************
** Description: 
** Collector model using efficiency curve efficiency
***************************************************************************

***********************************
** inputs from hydraulic solver
***********************************
EQUATIONS 2
TCollIn = @temp(input, TPiColIn)
MfrColl = ABS(@mfr(input, MfrPiColIn))

***********************************
** outputs to hydraulic solver
***********************************
EQUATIONS 1
TCollOut = [28,1]

***********************************
** outputs to other ddck
***********************************

******************************************************************************************
** outputs to energy balance in kWh and ABSOLUTE value
** Following this naming standard : 
** qSysIn_name, qSysOut_name, elSysIn_name, elSysOut_name
******************************************************************************************
EQUATIONS 1
qSysIn_Collector = PColl_kW  

***********************************
** Dependencies with other ddck
***********************************
EQUATIONS 1
pumpColOn = puColOn

CONSTANTS 2
C_tilt = slopeSurfUser_1		! @dependencyDdck Collector tilt angle / slope [°]
C_azim = aziSurfUSer_1    		! @dependencyDdck Collector azimuth  (0:s, 90:w, 270: e) [°]

EQUATIONS 4
**surface-8
IT_Coll_kJhm2 = IT_surfUser_1		! Incident total radiation on collector plane, kJ/hm2 
IB_Coll_kJhm2 = IB_surfUser_1  		! incident beam radiation on collector plane, kJ/hm2
ID_Coll_kJhm2 = ID_surfUser_1  		! diffuse and ground reflected irradiance on collector tilt
AI_Coll = AI_surfUser_1  			! incident angle on collector plane, °

EQUATIONS 5
IT_Coll_kW = IT_Coll_kJhm2/3600		! Incident total radiation on collector plane, kW/m2
IB_Coll_kW = IB_Coll_kJhm2/3600     ! incident beam radiation on collector plane, kW/m2
ID_Coll_kW = ID_Coll_kJhm2/3600     ! diffuse and ground reflected irradiance on collector tilt (kW/m2)
IT_Coll_Wm2 = IT_surfUser_1/3.6
IT_Coll_kWm2 = IT_surfUser_1/3600

***********************************
** Begin CONSTANTS
***********************************
CONSTANTS 3  
MfrCPriSpec = 15		! Coll. Prim. loop spec. mass flow [kg/hm2]
AcollAp = 5         	! Collector area  
MfrCPriNom = MfrCPriSpec*AcollAp

***********************************
** Begin TYPE
***********************************
UNIT 28 TYPE 1
PARAMETERS 11
nSeries       		! number in series
AcollAp       		! collector area
cpBri          		! fluid specific heat kj(kgK
efficiencyMode		! efficiency mode
testedMfr      		! tested flow rate kg/(hm2)
Eta0          		! intercept efficiency 
a1            		! efficiency slope kJ/hm^2K
a2            		! efficiency curvature kJ/hm^2K^2
2             		! optical mode
FirstOrderIAM  		! 1st order IAM
SecondOrderIAM		! 2nd order IAM
INPUTS 9
TCollIn
MfrColl
Tamb
IT_Coll_kJhm2
IT_H
ID_Coll_kJhm2
0,0
AI_Coll		!Flo check		! JS: This was defined wrong before (C_azim, even though it is incident angle input). Now it should be correct.
C_tilt 		!Flo check		! JS: This should be correct
*** INITIAL INPUT VALUES
20 0 10 0 0 0 GroundReflectance 45 0 

EQUATIONS 4
**MfrCout = [700,2]
Pcoll = [28,3]		!kJ/h
PColl_kW = Pcoll/3600
PColl_kWm2 = PColl_kW/(AcollAp+1e-30)   
PColl_Wm2  = PColl_kWm2*1000   

***********************************
** Monthly printer
***********************************
CONSTANTS 1
unitPrintSol = 31

ASSIGN temp\SOLAR_MO.Prt unitPrintSol 

UNIT 32 TYPE 46      
PARAMETERS 6   
unitPrintSol		! 1: Logical unit number, -
-1           		! 2: Logical unit for monthly summaries, -
1            		! 3: Relative or absolute start time. 0: print at time intervals relative to the simulation start time. 1: print at absolute time intervals. No effect for monthly integrations
-1           		! 4: Printing & integrating interval, h. -1 for monthly integration
1            		! 5: Number of inputs to avoid integration, -
1            		! 6: Output number to avoid integration
INPUTS 4
Time  Pcoll_kW  PColl_kWm2  IT_Coll_kWm2
**
Time  Pcoll_kW  PColl_kWm2  IT_Coll_kWm2

***********************************
** Hourly printer
***********************************
CONSTANTS 1
unitHourlyCol = 33

ASSIGN    temp\SOLAR_HR.Prt    unitHourlyCol     

UNIT 34 TYPE 46		! Printegrator Monthly Values for System
PARAMETERS 7    
unitHourlyCol		! 1: Logical unit number, -
-1            		! 2: Logical unit for monthly summaries, -
1             		! 3: Relative or absolute start time. 0: print at time intervals relative to the simulation start time. 1: print at absolute time intervals. No effect for monthly integrations
1             		! 4: Printing & integrating interval, h. -1 for monthly integration
2             		! 5: Number of inputs to avoid integration, -
4             		! 6: Output number to avoid integration
5             		! 7: Output number to avoid integration
INPUTS 6
Pcoll_kW  PColl_kWm2  IT_Coll_kWm2 TCollOut TCollIn MfrColl
**  
Pcoll_kW  PColl_kWm2  IT_Coll_kWm2 TCollOut TCollIn MfrColl

***********************************
** Online Plotter
***********************************
UNIT 103 TYPE 65		!Changed automatically
PARAMETERS 12     
4     				! 1: Nb. of left-axis variables
2     				! 2: Nb. of right-axis variables
0     				! 3: Left axis minimum
10     				! 4: Left axis maximum
0     				! 5: Right axis minimum
100  				! 6: Right axis maximum
nPlotsPerSim		! 7: Number of plots per simulation
12     				! 8: X-axis gridpoints
1     				! 9: Shut off Online w/o removing
-1     				! 10: Logical unit for output file
0     				! 11: Output file units
0     				! 12: Output file delimiter
INPUTS 6    
Pcoll_kW  PColl_kWm2  IT_Coll_kWm2  MfrColl
TCollOut TCollIn
Pcoll_kW  PColl_kWm2  IT_Coll_kWm2  MfrColl
TCollOut TCollIn
LABELS  3     
Power_and_Mfr 
Temperatures   
Collector

*******************************
**END Type1.ddck
*******************************